plugins {
    id 'java'
    id 'application'
}

group = 'ch.so.agi'
version = '0.0.1'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

dependencies {
    implementation project(':library')

    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.0'
    testImplementation 'org.assertj:assertj-core:3.26.3'
}

application {
    mainClass = 'ch.so.agi.cli.Gpkg2CloudFormatCli'
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

tasks.named('jar', Jar) {
    archiveFileName = 'gpkg2cloudformats-cli.jar'
    manifest {
        attributes 'Main-Class': 'ch.so.agi.cli.Gpkg2CloudFormatCli'
    }
    dependsOn ':library:jar'
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.named('distTar', Tar) {
    archiveFileName = 'gpkg2cloudformats-cli.tar'
}

tasks.named('distZip', Zip) {
    archiveFileName = 'gpkg2cloudformats-cli.zip'
}
