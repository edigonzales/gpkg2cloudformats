plugins {
    id 'java-library'
    id 'maven-publish'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

dependencies {
    api 'org.wololo:flatgeobuf:3.26.2'
    api 'org.locationtech.jts:jts-core:1.19.0'
    implementation 'org.xerial:sqlite-jdbc:3.46.0.0'
    implementation 'com.google.guava:guava:33.2.1-jre'
    implementation 'org.apache.parquet:parquet-hadoop:1.17.0'
    implementation 'org.apache.parquet:parquet-column:1.17.0'
    implementation 'org.apache.hadoop:hadoop-common:3.4.0'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.11.0'
    testImplementation 'org.assertj:assertj-core:3.26.3'
    testImplementation 'org.mockito:mockito-core:5.13.0'
    testImplementation 'org.apache.hadoop:hadoop-mapreduce-client-core:3.4.0'
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

tasks.named('jar', Jar) {
    archiveFileName = 'gpkg2cloudformats.jar'
}

def mavenUsername = providers.gradleProperty('mavenUsername')
        .orElse(providers.environmentVariable('MAVEN_USERNAME'))
def mavenPassword = providers.gradleProperty('mavenPassword')
        .orElse(providers.environmentVariable('MAVEN_PASSWORD'))

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = 'gpkg2cloudformats'
        }
    }
    repositories {
        maven {
            name = 'sogeoSnapshots'
            url = uri('https://jars.sogeo.services/snapshots')
            credentials(PasswordCredentials) {
                username = mavenUsername.orNull
                password = mavenPassword.orNull
            }
        }
    }
}

tasks.withType(PublishToMavenRepository).configureEach {
    doFirst {
        if (!mavenUsername.present || !mavenPassword.present) {
            throw new GradleException('Missing Maven credentials. Set mavenUsername/mavenPassword or MAVEN_USERNAME/MAVEN_PASSWORD.')
        }
    }
}
